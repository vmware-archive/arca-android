// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'java'

dependencies {
    compile project(":arca-app")
    compile project(":arca-core")
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}

allprojects {
    apply plugin: 'maven'

    group = 'io.pivotal'
    version = '1.0-beta.2-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    uploadArchives {
        repositories.mavenDeployer {

            def userName = System.getenv('MAVEN_USER')
            def password = System.getenv('MAVEN_PASS')

            repository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: userName, password: password)
            }
        }
    }
}

ext.pomInfo = {

    name 'Arca Android'
    description 'A comprehensive framework for caching and displaying data.'
    url 'https://github.com/cfmobile/arca-android'

    scm {
        url 'git@github.com:cfmobile/arca-android.git'
        connection 'scm:git:git@github.com:cfmobile/arca-android.git'
        developerConnection 'scm:git:git@github.com:cfmobile/arca-android.git'
    }

    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            id 'jwinters'
            name 'Joshua Winters'
            email 'jwinters@gopivotal.com'
        }
    }
}

configure([
    rootProject,
    project('arca-app'),
    project('arca-core')
]) {

    apply plugin: 'java'

    uploadArchives {
        repositories.mavenDeployer {
            pom.project pomInfo

            pom.whenConfigured {
                pom -> pom.packaging = 'pom'
            }
        }
    }
}

configure([
    project('arca-app:arca-adapters'),
    project('arca-app:arca-dispatcher'),
    project('arca-app:arca-fragments'),
    project('arca-app:arca-monitor'),
    project('arca-core:arca-broadcaster'),
    project('arca-core:arca-provider'),
    project('arca-core:arca-service'),
    project('arca-core:arca-threading'),
    project('arca-core:arca-utils')
]) {

    apply plugin: 'android-library'

    android {
        compileSdkVersion 19
        buildToolsVersion "19.0.3"

        defaultConfig {
            minSdkVersion 9
            targetSdkVersion 19
            versionCode 1
            versionName "1.0"
        }

        sourceSets {
            androidTest.setRoot('src/test')
        }
    }

    task jarClasses(type: Jar, dependsOn: assembleRelease) {
        from fileTree(dir: 'build/classes/release')
    }

    task jarSources(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java
    }

    task jarJavadoc(type: Jar) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives jarClasses
        archives jarSources
        archives jarJavadoc
    }

    uploadArchives {
        repositories.mavenDeployer {
            pom.project pomInfo
        }
    }
}
